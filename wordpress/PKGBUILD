developer=http://wordpress.org/
url=$developer
maintainer="http://indiecomputing.com/"
pkgname=$(basename $(pwd))
pkgver=4.8.3
pkgrel=1
pkgdesc="Blog tools, publishing platform, and CMS"
arch=('any')
license=("GPL")
source=("http://wordpress.org/wordpress-${pkgver}.tar.gz")
options=('!strip')
sha512sums=('696be1c24dec869d896e22b408b17009e8c482545237156f17d4b5f2d726876a17c3cc5c3de632126229747589d74f7b188c06d2a79878efc800bd6db273d709')

package() {
# Manifest
    mkdir -p $pkgdir/var/lib/ubos/manifests
    install -m0644 $startdir/ubos-manifest.json $pkgdir/var/lib/ubos/manifests/${pkgname}.json

# Icons
    mkdir -p $pkgdir/srv/http/_appicons/$pkgname
    install -m644 $startdir/appicons/{72x72,144x144}.png $pkgdir/srv/http/_appicons/$pkgname/

# Templates
    mkdir -p $pkgdir/usr/share/${pkgname}/tmpl
    install -m755 $startdir/tmpl/{htaccess,wp-content-htaccess}.tmpl $pkgdir/usr/share/${pkgname}/tmpl/
    install -m755 $startdir/tmpl/wp-config.pl $pkgdir/usr/share/${pkgname}/tmpl/

# Scripts
    mkdir -p $pkgdir/usr/share/${pkgname}/bin
    install -m755 $startdir/bin/{initialize,upgrade,activate-plugin,activate-theme}.pl $pkgdir/usr/share/${pkgname}/bin/

# Code
    cp -dr --no-preserve=ownership $srcdir/wordpress $pkgdir/usr/share/${pkgname}/
}
