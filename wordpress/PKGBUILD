developer=http://wordpress.org/
url=$developer
maintainer="http://indiecomputing.com/"
pkgname=$(basename $(pwd))
pkgver=4.8
pkgrel=1
pkgdesc="Blog tools, publishing platform, and CMS"
arch=('any')
license=("GPL")
source=("http://wordpress.org/wordpress-${pkgver}.tar.gz")
options=('!strip')
sha512sums=('20249cf13dbcec198df3ec1feb3a7381c194c7c9f832262e40449f90c5e5b5d3f4b63b6021e78c65cc615e059b1b601881d33ce03e1e4618231329b680511467')

package() {
# Manifest
    mkdir -p $pkgdir/var/lib/ubos/manifests
    install -m0644 $startdir/ubos-manifest.json $pkgdir/var/lib/ubos/manifests/${pkgname}.json

# Icons
    mkdir -p $pkgdir/srv/http/_appicons/$pkgname
    install -m644 $startdir/appicons/{72x72,144x144}.png $pkgdir/srv/http/_appicons/$pkgname/

# Templates
    mkdir -p $pkgdir/usr/share/${pkgname}/tmpl
    install -m755 $startdir/tmpl/{htaccess,wp-content-htaccess}.tmpl $pkgdir/usr/share/${pkgname}/tmpl/
    install -m755 $startdir/tmpl/wp-config.pl $pkgdir/usr/share/${pkgname}/tmpl/

# Scripts
    mkdir -p $pkgdir/usr/share/${pkgname}/bin
    install -m755 $startdir/bin/{initialize,upgrade,activate-plugin,activate-theme}.pl $pkgdir/usr/share/${pkgname}/bin/

# Code
    cp -dr --no-preserve=ownership $srcdir/wordpress $pkgdir/usr/share/${pkgname}/
}
