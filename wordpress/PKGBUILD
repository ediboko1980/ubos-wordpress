developer=http://wordpress.org/
url=$developer
maintainer="http://indiecomputing.com/"
pkgname=$(basename $(pwd))
pkgver=4.7.3
pkgrel=2
pkgdesc="Blog tools, publishing platform, and CMS"
arch=('any')
license=("GPL")
source=("http://wordpress.org/wordpress-${pkgver}.tar.gz")
options=('!strip')
sha512sums=('071df65c3a43557faf351838a661a83e26d8de37c8633dc17a59d773cc91caef640a625b0719606df1fc563fd18af71ba1da22a5b6f345339e73761754484dc5')

package() {
# Manifest
    mkdir -p $pkgdir/var/lib/ubos/manifests
    install -m0644 $startdir/ubos-manifest.json $pkgdir/var/lib/ubos/manifests/${pkgname}.json

# Icons
    mkdir -p $pkgdir/srv/http/_appicons/$pkgname
    install -m644 $startdir/appicons/{72x72,144x144}.png $pkgdir/srv/http/_appicons/$pkgname/

# Templates
    mkdir -p $pkgdir/usr/share/${pkgname}/tmpl
    install -m755 $startdir/tmpl/{htaccess,wp-content-htaccess}.tmpl $pkgdir/usr/share/${pkgname}/tmpl/
    install -m755 $startdir/tmpl/wp-config.pl $pkgdir/usr/share/${pkgname}/tmpl/

# Scripts
    mkdir -p $pkgdir/usr/share/${pkgname}/bin
    install -m755 $startdir/bin/{initialize,upgrade,activate-plugin,activate-theme}.pl $pkgdir/usr/share/${pkgname}/bin/

# Code
    cp -dr --no-preserve=ownership $srcdir/wordpress $pkgdir/usr/share/${pkgname}/
}
